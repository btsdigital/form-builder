plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id 'java-library'
    id "maven-publish"
    id "io.gitlab.arturbosch.detekt" version "1.0.1"
    id 'signing'
}

group 'kz.btsd.bot'
ext.moduleName = 'kz.btsd.bot.form-builder'

version version

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    testImplementation "junit:junit:$junitVersion"
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

detekt {
    config = files("./detekt-config.yml")
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileKotlin.destinationDir = compileJava.destinationDir

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            version = version
            from components.java
        }
    }
}


def repo = "github.com/btsdigital/bot-sdk"

if (project.hasProperty("jiraUsername") && project.hasProperty("jiraPass")) {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId 'kz.btsd'
                artifactId 'form-builder'
                version version
                from components.java
                artifact sourceJar
                artifact javadocJar

                pom {

                    description = 'form builder for AITU messenger bots'
                    name = 'AITU bots form builder'
                    url = repo
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = "https://$repo/blob/master/LICENSE"
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = "https://$repo"
                        connection = "scm:git:git://$repo\\.git"
                    }
                    developers {
                        developer {
                            name = 'btsd bot platform'
                            organization = "BTSD"
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username jiraUsername
                    password jiraPass
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }
    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}
